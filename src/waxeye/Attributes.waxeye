# Attributes grammar for use with Waxeye Parser Generator
# (C) Kyle Kamperschroer 2013

#import common.waxeye

ATTRIBUTES <- +ATTRIBUTE
ATTRIBUTE <- ( YIELD |
               COOK_TIME |
               PREP_TIME |
               COOL_TIME |
               OVERALL_TIME
             )
             ?( +[|,- ] |
                wsn
              )

YIELD <- YIELD_PREFIX ?wsn
         ?(ATTRIBUTE_SEPARATOR wsn)
         ?(YIELD_PREFIX wsn)
         AMOUNT ?ws ?(UNIT | WORDS_NO_BREAK)

YIELD_PREFIX <- ( "yield"?"s" |
                  "serve"?"s" |
                  "make"?"s" |
                  "serving"?"s"
                )
                ?wsn

COOK_TIME <- COOK_TIME_PREFIX wsn
             ?(ATTRIBUTE_SEPARATOR wsn)
             +(AMOUNT ws TIME_UNITS ?(?"," ws))

COOK_TIME_PREFIX <- ( "cooking" |
                      "cook" |
                      "bake" |
                      "grill"
                    ) wsn
                    ?("time")

COOL_TIME <- COOL_TIME_PREFIX wsn
             ?(ATTRIBUTE_SEPARATOR wsn)
             +(AMOUNT ws TIME_UNITS ?(?"," ws))

COOL_TIME_PREFIX <- ( "cooling" |
                      "cool" |
                      "sit"
                    ) wsn
                    ?("time")

TIME_UNITS <- ( "hour"?"s" |
                "minute"?"s" |
                "min"?"s"?"." |
                "h"?"s"?"." |
                "m"?"s"?"." 
              )
              ?wsn

PREP_TIME <- PREP_TIME_PREFIX wsn
             ?(ATTRIBUTE_SEPARATOR wsn)
             +(AMOUNT ws TIME_UNITS ?(?"," ws))

PREP_TIME_PREFIX <- ( "prep" |
                      "preparation" |
                      "hands-on" |
                      "hands on" |
                      "active"
                    ) wsn
                    ?("time")

OVERALL_TIME <- OVERALL_TIME_PREFIX wsn
                ?(ATTRIBUTE_SEPARATOR wsn)
                +(AMOUNT ws TIME_UNITS ?(?"," ws))

OVERALL_TIME_PREFIX <- ( "overall" |
                         "total" |
                         "ready in"
                       ) wsn
                       ?("time")

ATTRIBUTE_SEPARATOR <= (":" | "=" | "is")
